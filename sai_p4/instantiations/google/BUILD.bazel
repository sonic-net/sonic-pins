# Copyright 2020 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@com_github_p4lang_p4c//:bazel/p4_library.bzl", "p4_library")
load("@com_google_googleapis_imports//:imports.bzl", "cc_proto_library")
load("//gutil/gutil:diff_test.bzl", "cmd_diff_test", "diff_test")
load("//gutil/gutil/embed_data:build_defs.bzl", "cc_embed_data")
load("//p4_infra/p4_pdpi:pdgen.bzl", "p4_pd_proto")

package(
    default_visibility = ["//visibility:public"],
    licenses = ["notice"],
)

# -- P4 program ----------------------------------------------------------------

SHARED_DEPS = [
    "acl_common_actions.p4",
    "acl_egress.p4",
    "acl_ingress.p4",
    "acl_pre_ingress.p4",
    "ids.h",
    "versions.h",
    "roles.h",
    "bitwidths.p4",
    "minimum_guaranteed_sizes.h",
    "admit_google_system_mac.p4",
    "//sai_p4/fixed",
]

MIDDLEBLOCK_DEPS = SHARED_DEPS + ["middleblock.p4"]

FABRIC_BORDER_ROUTER_DEPS = SHARED_DEPS + [
    "fabric_border_router.p4",
]

TOR_DEPS = SHARED_DEPS + [
    "tor.p4",
]

WBB_DEPS = [
    "acl_wbb_ingress.p4",
    "ids.h",
    "roles.h",
    "versions.h",
    "bitwidths.p4",
    "minimum_guaranteed_sizes.h",
    "//sai_p4/fixed",
]

INSTANTIATIONS = [
    # (<instantiation>, <dependencies>)
    ("fabric_border_router", FABRIC_BORDER_ROUTER_DEPS),
    ("middleblock", MIDDLEBLOCK_DEPS),
    ("tor", TOR_DEPS),
    ("wbb", WBB_DEPS),
]

[
    p4_library(
        name = instantiation,
        src = "%s.p4" % instantiation,
        # Do not access directly. Use the C++/Go APIs in the "P4Info" section of
        # this BUILD file instead.
        p4info_out = "generated/%s.p4info.pb.txt" % instantiation,
        deps = deps,
    )
    for (instantiation, deps) in INSTANTIATIONS
]

cc_library(
    name = "minimum_guaranteed_sizes",
    hdrs = ["minimum_guaranteed_sizes.h"],
)

cc_library(
    name = "p4_ids",
    hdrs = ["ids.h"],
    deps = [
        "//sai_p4/fixed:p4_ids",
    ],
)

cc_library(
    name = "p4_versions",
    hdrs = ["versions.h"],
)

cc_library(
    name = "p4_roles",
    hdrs = ["roles.h"],
)

# -- PD proto for SDN controller -----------------------------------------------

proto_library(
    name = "sai_pd_proto",
    srcs = ["sai_pd.proto"],
    deps = [
        "@com_github_p4lang_p4runtime//:p4runtime_proto",
        "@com_google_googleapis//google/rpc:code_proto",
        "@com_google_googleapis//google/rpc:status_proto",
    ],
)

cc_proto_library(
    name = "sai_pd_cc_proto",
    deps = [":sai_pd_proto"],
)

build_test(
    name = "sai_pd_proto_test",
    targets = [
        ":sai_pd_proto",
        ":sai_pd_cc_proto",
    ],
)

cc_library(
    name = "sai_pd_util",
    srcs = ["sai_pd_util.cc"],
    hdrs = ["sai_pd_util.h"],
    deps = [
        ":sai_pd_cc_proto",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/types:optional",
    ],
)

cc_test(
    name = "sai_pd_util_test",
    srcs = ["sai_pd_util_test.cc"],
    deps = [
        ":sai_pd_cc_proto",
        ":sai_pd_util",
        "//gutil/gutil:testing",
        "@com_google_absl//absl/strings",
        "@com_google_googletest//:gtest_main",
        "@com_googlesource_code_re2//:re2",
    ],
)

# Ensures that the checked in and generated unioned P4Infos match.
# To update the checked in unioned P4Info:
#   blaze run :union_p4info_up_to_date_test -- --update
cmd_diff_test(
    name = "union_p4info_up_to_date_test",
    actual_cmd = "$(execpath {p4info_union}) --list_of_p4infos='{p4infos}'".format(
        p4info_union = "//p4_infra/p4_pdpi:p4info_union",
        p4infos = ",".join([
            "$(location %s.p4info.pb.txt)" % instantiation
            for (instantiation, _) in INSTANTIATIONS
        ]),
    ),
    data = [
        "%s.p4info.pb.txt" % instantiation
        for (instantiation, _) in INSTANTIATIONS
    ],
    expected = "unioned_p4info.pb.txt",
    tools = ["//p4_infra/p4_pdpi:p4info_union"],
)

# Only for testing and updating the PD proto when the P4 program changes.
p4_pd_proto(
    name = "generated_sai_pd",
    src = "unioned_p4info.pb.txt",
    # Do not access directly. Use checked in sai_pd.proto instead.
    out = "generated/sai_pd.proto",
    package = "sai",
    roles = [
        "sdn_controller",
    ],
)

# -- P4Info --------------------------------------------------------------------

# The P4Info proto, in C++ format.
cc_library(
    name = "sai_p4info_cc",
    srcs = ["sai_p4info.cc"],
    hdrs = ["sai_p4info.h"],
    deps = [
        ":instantiations",
        ":sai_p4info_fetcher_cc",
        "//p4_infra/p4_pdpi:ir",
        "//p4_infra/p4_pdpi:ir_cc_proto",
        "//sai_p4/tools:p4info_tools",
        "@com_github_google_glog//:glog",
        "@com_github_p4lang_p4runtime//:p4info_cc_proto",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_protobuf//:protobuf",
    ],
)

cc_test(
    name = "sai_p4info_test",
    srcs = ["sai_p4info_test.cc"],
    deps = [
        ":instantiations",
        ":sai_p4info_cc",
        "//gutil/gutil:proto_matchers",
        "//gutil/gutil:status_matchers",
        "//gutil/gutil:version",
        "@com_github_p4lang_p4_constraints//p4_constraints/backend:constraint_info",
        "@com_github_p4lang_p4runtime//:p4info_cc_proto",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/types:optional",
        "@com_google_absl//absl/types:variant",
        "@com_google_googletest//:gtest_main",
        "@com_google_protobuf//:protobuf",
    ],
)

cc_library(
    name = "sai_p4info_fetcher_cc",
    srcs = ["sai_p4info_fetcher.cc"],
    hdrs = ["sai_p4info_fetcher.h"],
    deps =
        [instantiation + "_p4info_embed" for (instantiation, _) in INSTANTIATIONS] + [
            ":clos_stage",
            ":instantiations",
            ":unioned_p4info_embed",
            "//p4_infra/p4_pdpi:ir_cc_proto",
            "//sai_p4/tools:p4info_tools",
            "@com_github_google_glog//:glog",
            "@com_github_p4lang_p4runtime//:p4info_cc_proto",
            "@com_google_absl//absl/status",
            "@com_google_absl//absl/strings",
            "@com_google_protobuf//:protobuf",
        ],
)

cc_test(
    name = "sai_p4info_fetcher_test",
    srcs = ["sai_p4info_fetcher_test.cc"],
    deps = [
        ":clos_stage",
        ":instantiations",
        ":sai_p4info_fetcher_cc",
        "//gutil/gutil:proto_matchers",
        "//gutil/gutil:status_matchers",
        "@com_github_p4lang_p4runtime//:p4info_cc_proto",
        "@com_google_absl//absl/container:flat_hash_set",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/strings",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "instantiations",
    srcs = ["instantiations.cc"],
    hdrs = ["instantiations.h"],
    deps = [
        "@com_github_google_glog//:glog",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/strings:str_format",
    ],
)

[
    [
        # Ensures that the checked in and compile-time generated P4Infos match.
        # To update the checked in P4Info:
        #   blaze run :<instantiation>_p4info_up_to_date_test -- --update
        diff_test(
            name = "%s_p4info_up_to_date_test" % instantiation,
            actual = "generated/%s.p4info.pb.txt" % instantiation,  # Generated.
            expected = "%s.p4info.pb.txt" % instantiation,  # Checked in.
        ),

        # Auxiliary targets, see go/totw/128.
        cc_embed_data(
            name = "%s_p4info_embed" % instantiation,
            srcs = ["%s.p4info.pb.txt" % instantiation],
            cc_file_output = "%s_p4info_embed.cc" % instantiation,
            cpp_namespace = "sai",
            h_file_output = "%s_p4info_embed.h" % instantiation,
        ),
    ]
    for (instantiation, _) in INSTANTIATIONS
]

cc_embed_data(
    name = "unioned_p4info_embed",
    srcs = [
        "unioned_p4info.pb.txt",
    ],
    cc_file_output = "unioned_p4info_embed.cc",
    cpp_namespace = "sai",
    h_file_output = "unioned_p4info_embed.h",
)

# -- Non-standard platforms ----------------------------------------------------

MACRO_BY_NONSTANDARD_PLATFORM_NAME = {
    # (<name>, <macro>)
    "bmv2": "PLATFORM_BMV2",
    "p4_symbolic": "PLATFORM_P4SYMBOLIC",
}

[
    p4_library(
        name = "sai_%s_%s" % (instantiation, platform_name),
        src = "%s.p4" % instantiation,
        extra_args = "-D%s" % platform_macro,
        p4info_out = "generated/sai_%s_%s.p4info.pb.txt" % (instantiation, platform_name),
        target = "bmv2",
        target_out = "generated/sai_%s_%s.config.json" % (instantiation, platform_name),
        visibility = [
            ":__pkg__",
            # TODO: Legacy, remove.
            "//platforms/networking/gpins/testing/blackbox/p4:__pkg__",
        ],
        deps = deps,
)
    for (instantiation, deps) in INSTANTIATIONS
    for (platform_name, platform_macro) in MACRO_BY_NONSTANDARD_PLATFORM_NAME.items()
]

build_test(
    name = "sai_nonstandard_platforms_build_test",
    targets = [
        ":sai_%s_%s" % (instantiation, platform)
        for (instantiation, _) in INSTANTIATIONS
        for (platform, _) in MACRO_BY_NONSTANDARD_PLATFORM_NAME.items()
    ],
)

# P4 info and config for non-standard platforms, accessible via C++ API.
cc_library(
    name = "sai_nonstandard_platforms_cc",
    srcs = ["sai_nonstandard_platforms.cc"],
    hdrs = ["sai_nonstandard_platforms.h"],
    deps = [
        ":instantiations",
        ":sai_nonstandard_platforms_embed",
        "//p4_infra/p4_pdpi:ir_cc_proto",
        "@com_github_google_glog//:glog",
        "@com_github_p4lang_p4runtime//:p4info_cc_proto",
        "@com_github_p4lang_p4runtime//:p4runtime_cc_proto",
        "@com_google_absl//absl/container:flat_hash_map",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/strings:str_format",
        "@com_google_protobuf//:protobuf",
    ],
)

cc_library(
    name = "clos_stage",
    srcs = ["clos_stage.cc"],
    hdrs = ["clos_stage.h"],
    deps = [
        ":instantiations",
        "@com_github_google_glog//:glog",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/strings",
    ],
)

cc_test(
    name = "clos_stage_test",
    srcs = ["clos_stage_test.cc"],
    deps = [
        ":clos_stage",
        ":instantiations",
        "//gutil/gutil:status_matchers",
        "//gutil/gutil:testing",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_library(
    name = "annotations",
    hdrs = ["annotations.h"],
    deps = [
        "@com_google_absl//absl/strings",
    ],
)

cc_library(
    name = "default_wcmp_capacities",
    hdrs = ["default_wcmp_capacities.h"],
    deps = [
        ":minimum_guaranteed_sizes",
        "//sai_p4/tools:p4info_tools",
    ],
)

# Auxiliary target, see go/totw/128.
cc_embed_data(
    name = "sai_nonstandard_platforms_embed",
    srcs = [
        "generated/sai_%s_%s.%s" % (instantiation, platform, extension)
        for (instantiation, _) in INSTANTIATIONS
        for (platform, _) in MACRO_BY_NONSTANDARD_PLATFORM_NAME.items()
        for extension in [
            "config.json",
            "p4info.pb.txt",
        ]
    ],
    cc_file_output = "sai_nonstandard_platforms_embed.cc",
    cpp_namespace = "sai",
    flatten = True,
    h_file_output = "sai_nonstandard_platforms_embed.h",
)

cc_test(
    name = "sai_nonstandard_platforms_cc_test",
    srcs = ["sai_nonstandard_platforms_test.cc"],
    deps = [
	":instantiations",
        ":sai_nonstandard_platforms_cc",
        "//gutil/gutil:status_matchers",
        "@com_github_p4lang_p4runtime//:p4info_cc_proto",
        "@com_google_absl//absl/strings",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_binary(
    name = "sai_nonstandard_platforms_config_generator",
    srcs = ["sai_nonstandard_platforms_config_generator.cc"],
    deps = [
        ":instantiations",
        ":sai_nonstandard_platforms_cc",
        "//gutil/gutil:proto",
        "@com_github_p4lang_p4runtime//:p4runtime_cc_proto",
        "@com_google_absl//absl/flags:flag",
        "@com_google_absl//absl/flags:parse",
    ],
)

filegroup(
    name = "sai_p4",
    srcs = glob(["**/*.p4"]) + glob(["**/*.h"]),
)

[
    # In cases where we are unable to access P4C's internal preprocessor ('cc') we need to
    # preprocess the P4 program of a particular instantiation.
    genrule(
        name = "preprocessed_%s_%s" % (platform, instantiation),
        srcs = [
            "@com_github_p4lang_p4c//:p4include",
            "@com_github_p4lang_p4c//:p4include/v1model.p4",
            "//sai_p4/instantiations/google:%s.p4" % instantiation,
            "//sai_p4/instantiations/google:sai_p4",
            "//sai_p4/fixed",
        ],
        outs = ["preprocessed_%s_%s.p4" % (platform, instantiation)],
        cmd = """
              # genrules do not support retrieval of directories from dependencies, so we need to use a workaround. 
              # We depend on v1model.p4 and retrieve its parent directory. 
              P4INCLUDE_DIR=$$(dirname $(location @com_github_p4lang_p4c//:p4include/v1model.p4))
              $(CC) -E -x c -Wno-comment -C -undef -nostdinc -x assembler-with-cpp -I$$P4INCLUDE_DIR -I. %s \
              $(location //sai_p4/instantiations/google:%s.p4) > $(OUTS)
              """ % (
            "-D" + MACRO_BY_NONSTANDARD_PLATFORM_NAME[platform] if platform in MACRO_BY_NONSTANDARD_PLATFORM_NAME else "",
            instantiation,
        ),
        toolchains = ["@bazel_tools//tools/cpp:current_cc_toolchain"],
    )
    for (instantiation, deps) in INSTANTIATIONS
    for platform in ("standard", "bmv2")
]

filegroup(
    name =
        "preprocessed_sai_programs",
    srcs = [
        "//sai_p4/instantiations/google:preprocessed_%s_%s.p4" % (platform, instantiation)
        for (instantiation, _) in INSTANTIATIONS
        for platform in ("standard", "bmv2")
    ],
)

cc_test(
    name = "preprocessed_sai_programs_test",
    srcs = ["preprocessed_sai_programs_test.cc"],
    data = [":preprocessed_sai_programs"],
    deps = [
        ":instantiations",
        ":sai_nonstandard_platforms_cc",
        "@com_google_absl//absl/strings",
        "@com_google_googletest//:gtest_main",
    ],
)

